[script]
n0=menu channel,menubar,nicklist {
n1=  Nick/Address Tracking
n2=  .$iif($group(#Addtrack) == on,$style(1)) Show used nicks on join (*@host): {
n3=    var %status = $iif($group(#Addtrack) == on,Disable,Enable)
n4=    $+(.,%status) #Addtrack
n5=    echo 7 -at * Users previous nicks will $iif(%status == Disable,no longer,now) be shown when they join.
n6=  }
n7=  .-
n8=  .$iif($1,Check nicks for $1):CheckNicks $1
n9=  .Check nicks for an address: {
n10=    CheckNicks $+(*!,$$?="Enter the hostmask you want to check in the format NICK!IDENT@HOST*")
n11=  }
n12=}
n13=
n14=on *:start: {
n15=  hmake addtrack 100
n16=  if ($isfile(addtrack.hsh)) hload addtrack addtrack.hsh
n17=}
n18=
n19=on *:exit: { 
n20=  if ($hget(addtrack)) hsave -o addtrack addtrack.hsh
n21=}
n22=
n23=#Addtrack on
n24=on *:join:#: {
n25=  if ($wildsite iswm $address($me,5)) return
n26=  var %mask = $mask($fulladdress,2)
n27=  if ($hget(addtrack,%mask)) {
n28=    var %usednicks = $ifmatch
n29=    if (%usednicks != $nick) && ($group(#Addtrack) == on) echo 10 -t $chan [Nick Tracking (*@host)] $nick has also used the nick[s]:  7 $+ $remtok(%usednicks,$nick,1,44)
n30=    if (!$istok(%usednicks,$nick,44)) hadd -m addtrack %mask $addtok(%usednicks,$nick,44)
n31=    if ($numtok(%usednicks,44) > 10) hadd -m addtrack %mask $deltok(%usednicks,1,44)
n32=  }
n33=  else {
n34=    hadd -m addtrack %mask $nick
n35=  }
n36=}
n37=
n38=on *:nick: {
n39=  if ($wildsite iswm $address($me,5)) return
n40=  var %mask = $mask($fulladdress,2)
n41=  if ($hget(addtrack,%mask)) {
n42=    var %usednicks = $ifmatch
n43=    if (!$istok(%usednicks,$newnick,44)) hadd -m addtrack %mask $addtok(%usednicks,$newnick,44)
n44=    if ($numtok(%usednicks,44) > 10) hadd -m addtrack %mask $deltok(%usednicks,1,44)
n45=  }
n46=  else {
n47=    hadd -m addtrack %mask $newnick
n48=  }
n49=}
n50=#Addtrack end
n51=
n52=alias CheckNicks {
n53=  if (*!*@* iswm $1) {
n54=    var %mask = $mask($1,2)
n55=    if (!$hget(addtrack,%mask)) echo 4 -ta [Nick Tracking (*@host)] No entry found for $1
n56=    else echo 10 -ta Address $1 has used the nick[s]: 7 $+ $hget(addtrack,$1)
n57=  }
n58=  elseif (!$hget(addtrack,$address($1,2))) echo 4 -ta [Nick Tracking (*@host)] No entry found for $1
n59=  elseif ($hget(addtrack,$address($1,2)) != $1) {
n60=    var %usednicks = $ifmatch
n61=    echo 10 -ta [Nick Tracking (*@host)] $1 has also used the nick[s]: 7 $+ $remtok(%usednicks,$1,1,43)
n62=  }
n63=  else echo 10 -ta [Nick Tracking (*@host)] $1 has not been seen using any other nicks.
n64=}
