[script]
n0=; ===============================
n1=; TVRage Script v1 by ES and hxck
n2=; ===============================
n3=
n4=on *:LOAD: {
n5=  /echo -s TVRage v1 :: $chr(91) ES & hxck $chr(93) has been loaded. Type !tvhelp for a list of commands. http://www.hawkee.com/snippet/8822/
n6=}
n7=
n8=on *:TEXT:!tvhelp:#: {
n9=  msg # !n|!next - Get next episode information (ie !n Dexter)
n10=  msg # !l|!last - Get last episode information (ie !l Dexter)
n11=  msg # !tv - Get general show information (ie !tv Dexter)
n12=  msg # !ep seasonXepisode show - Get episode summary (ie !ep 1x2 Dexter)
n13=  msg # !show - Get show summary (ie !show Dexter)
n14=}
n15=
n16=menu Channel,Status {
n17=  .$iif($group(#TVR) == On,$style(1)) ES/hxck
n18=  ..$iif($group(#TVR) == On,$style(2)) TVRage On: .enable #TVR
n19=  ..$iif($group(#TVR) == Off,$style(2)) TVRage Off: .disable #TVR
n20=}
n21=#TVR off
n22=on *:TEXT:!*:#: {
n23=
n24=  ; ===========================
n25=  ; Close sockets if we need to
n26=  ; ===========================
n27=
n28=  if ($sock(tvrage)) { sockclose tvrage }
n29=  if ($sock(tvrage2)) { sockclose tvrage2 }
n30=  if ($sock(tvrage3)) { sockclose tvrage3 }
n31=
n32=  ; ===========================
n33=  ; Set different triggers here
n34=  ; ===========================
n35=
n36=  if (($1 == !n) || ($1 == !next)) {
n37=    if (!$2) { msg # Syntax: !n <show name> }
n38=    if ($2) {
n39=      set %tvr.output next
n40=      set %tvr.show $replace($2-,$chr(32),$chr(37) $+ 20)
n41=      sockopen -n tvrage services.tvrage.com 80
n42=      sockmark tvrage msg #
n43=    }
n44=  }
n45=  if (($1 == !l) || ($1 == !last)) {
n46=    if (!$2) { msg # Syntax: !l <show name> }
n47=    if ($2) {
n48=      set %tvr.output last
n49=      set %tvr.show $replace($2-,$chr(32),$chr(37) $+ 20)
n50=      sockopen -n tvrage services.tvrage.com 80
n51=      sockmark tvrage msg #
n52=    }
n53=  }
n54=  if ($1 == !tv) {
n55=    if (!$2) { msg # Syntax: !tv <show name> }
n56=    if ($2) {
n57=      set %tvr.output info
n58=      set %tvr.show $replace($2-,$chr(32),$chr(37) $+ 20)
n59=      sockopen -n tvrage services.tvrage.com 80
n60=      sockmark tvrage msg #
n61=    }
n62=  }
n63=  if ($1 == !ep) {
n64=    if (!$2) { msg # Syntax: !ep <ep number, eg 1x1> <show name> | halt }
n65=    if (!$3) { msg # Syntax: !ep <ep number, eg 1x1> <show name> | halt }
n66=    else {
n67=      set %tvr.output.nick $nick
n68=      set %tvr.output epsummary
n69=      set %tvr.show $replace($3-,$chr(32),$chr(37) $+ 20)
n70=      set %tvr.epnum $2
n71=      sockopen -n tvrage services.tvrage.com 80
n72=      sockmark tvrage msg #
n73=    }
n74=  }
n75=  if ($1 == !show) {
n76=    if (!$2) { msg # Syntax: !show <show name> }
n77=    else {
n78=      set %tvr.output.nick $nick
n79=      set %tvr.show $replace($2-,$chr(32),$chr(37) $+ 20)
n80=      set %tvr.output showsummary
n81=      sockopen -n tvrage services.tvrage.com 80
n82=      sockmark tvrage msg #
n83=    }
n84=  }
n85=  else halt
n86=}
n87=
n88=; ===========================
n89=; Handle sockopen events here
n90=; ===========================
n91=
n92=on *:SOCKOPEN:tvrage: {
n93=  var %s sockwrite -n $sockname 
n94=  if ($sockerr) { $sock(tvrage).mark Socket Error: $sock(tvrage).wsmsg }
n95=  %s GET /tools/quickinfo.php?show= $+ %tvr.show $+ &ep= $+ %tvr.epnum HTTP/1.1
n96=  %s Host: services.tvrage.com
n97=  %s Connection: close
n98=  %s $crlf
n99=}
n100=
n101=on *:SOCKOPEN:tvrage2: {
n102=  var %s sockwrite -n $sockname 
n103=  if ($sockerr) { $sock(tvrage2).mark Socket Error: $sock(tvrage2).wsmsg }
n104=  %s GET %tvr.epsumurl HTTP/1.1
n105=  %s Host: www.tvrage.com
n106=  %s Connection: close
n107=  %s $crlf
n108=}
n109=
n110=on *:SOCKOPEN:tvrage3: {
n111=  var %s sockwrite -n $sockname 
n112=  if ($sockerr) { $sock(tvrage3).mark Socket Error: $sock(tvrage3).wsmsg }
n113=  %s GET /myfeeds/showinfo.php?key=sqsTSaJG4qJagM6ldpKZ&sid= $+ %tvr.showid HTTP/1.1
n114=  %s Host: services.tvrage.com
n115=  %s Connection: close
n116=  %s $crlf
n117=}
n118=
n119=; ===========================
n120=; Handle sockread events here
n121=; ===========================
n122=
n123=on *:SOCKREAD:tvrage: {
n124=  if ($sockerr) { $sock(tvrage).mark Socket Error: $sock(tvrage).wsmsg }
n125=  var %tvrtemp
n126=  sockread %tvrtemp
n127=
n128=  ; ============================
n129=  ; Set !n !l !tv variables here
n130=  ; ============================
n131=  if ((%tvr.output == next) || (%tvr.output == last) || (%tvr.output == info)) {
n132=    if (Show Name isin %tvrtemp) { set %tvr.showname $gettok(%tvrtemp,2,64) }
n133=    if (Show URL isin %tvrtemp) { set %tvr.url $gettok(%tvrtemp,2,64) }
n134=    if (Premiered isin %tvrtemp) { set %tvr.premiered $gettok(%tvrtemp,2,64) }
n135=    if (Started isin %tvrtemp) { set %tvr.started $gettok(%tvrtemp,2,64) }
n136=    if (Ended isin %tvrtemp) { set %tvr.ended $gettok(%tvrtemp,2,64) }
n137=    if (Country isin %tvrtemp) { set %tvr.country $gettok(%tvrtemp,2,64) }
n138=    if (Status isin %tvrtemp) { set %tvr.status $gettok(%tvrtemp,2,64) }
n139=    if (Classification isin %tvrtemp) { set %tvr.class $gettok(%tvrtemp,2,64) }
n140=    if (Genres isin %tvrtemp) { set %tvr.genres $gettok(%tvrtemp,2,64) }
n141=    if (Network isin %tvrtemp) { set %tvr.network $gettok(%tvrtemp,2,64) }
n142=    if (Airtime isin %tvrtemp) { set %tvr.airtime $gettok(%tvrtemp,2,64) }
n143=    if (Runtime isin %tvrtemp) { set %tvr.runtime $gettok(%tvrtemp,2,64) }
n144=    if (Next Episode isin %tvrtemp) {
n145=      set %tvr.nextemp $gettok(%tvrtemp,2,64)
n146=      set %tvr.nextepnum $gettok(%tvr.nextemp,1,94)
n147=      set %tvr.nextepname $gettok(%tvr.nextemp,2,94)
n148=      set %tvr.nextepday $gettok(%tvr.nextemp,3,94)
n149=    }
n150=    if (Latest Episode isin %tvrtemp) {
n151=      set %tvr.lastemp $gettok(%tvrtemp,2,64)
n152=      set %tvr.lastepnum $gettok(%tvr.lastemp,1,94)
n153=      set %tvr.lastepname $gettok(%tvr.lastemp,2,94)
n154=      set %tvr.lastepday $gettok(%tvr.lastemp,3,94)
n155=    }
n156=    if (GMT+0 NODST isin %tvrtemp) { set %tvr.airnextepcmt $calc($gettok(%tvrtemp,2,64) + 7200 - $ctime) }
n157=  }
n158=  ; ==============================
n159=  ; Set 1st !tvinfo variables here
n160=  ; ==============================
n161=
n162=  if (%tvr.output == showsummary) {
n163=    if (Show ID isin %tvrtemp) { set %tvr.showid $gettok(%tvrtemp,2,64) }
n164=    if (Show Name isin %tvrtemp) { set %tvr.showname $gettok(%tvrtemp,2,64) }
n165=    if (Show URL isin %tvrtemp) { set %tvr.url $gettok(%tvrtemp,2,64) }
n166=  }
n167=
n168=  ; ==========================
n169=  ; Set 1st !ep variables here
n170=  ; ==========================
n171=
n172=  if (%tvr.output == epsummary) {
n173=    if (Episode URL isin %tvrtemp) {
n174=      set %tvr.url $gettok(%tvrtemp,2,64)
n175=      set %tvr.epsumurl $remove($gettok(%tvrtemp,2,64),http://www.tvrage.com)
n176=    }
n177=    if (Episode info isin %tvrtemp) {
n178=      set %tvr.epnumout $gettok($gettok(%tvrtemp,2,64),1,94)
n179=      set %tvr.epname $gettok($gettok(%tvrtemp,2,64),2,94)
n180=      set %tvr.epair $gettok($gettok(%tvrtemp,2,64),3,94)
n181=    }
n182=    if (Show Name isin %tvrtemp) { set %tvr.showname $gettok(%tvrtemp,2,64) }
n183=  }
n184=}
n185=
n186=on *:SOCKREAD:tvrage2: {
n187=  if ($sockerr) { $sock(tvrage).mark Socket Error: $sock(tvrage2).wsmsg }
n188=
n189=  ; ==========================
n190=  ; Set 2nd !ep variables here
n191=  ; ==========================
n192=
n193=  var %tvrtemp2
n194=  sockread %tvrtemp2
n195=  if (%tvr.epsum.temp == 1) { 
n196=    set %tvr.epsum %tvrtemp2
n197=    set %tvr.epsum.temp 0
n198=  }
n199=  if (<div class='show_synopsis'> isin %tvrtemp2) { set %tvr.epsum.temp 1 }
n200=}
n201=
n202=on *:SOCKREAD:tvrage3: {
n203=  if ($sockerr) { $sock(tvrage).mark Socket Error: $sock(tvrage3).wsmsg }
n204=
n205=  ; ==============================
n206=  ; Set 2nd !tvinfo variables here
n207=  ; ==============================
n208=
n209=  var %tvrtemp3
n210=  sockread %tvrtemp3
n211=  if (<image> isin %tvrtemp3) { set %tvr.showpic $between(%tvrtemp3,<image>,</image>,1) }
n212=  if (<summary> isin %tvrtemp3) { set %tvr.showsummary $between(%tvrtemp3,<summary>,</summary>,1) }
n213=}
n214=
n215=; ============================
n216=; Handle sockclose events here
n217=; ============================
n218=
n219=on *:SOCKCLOSE:tvrage: {
n220=
n221=  ; ============================
n222=  ; Next Episode
n223=  ; ============================
n224=
n225=  if (%tvr.output == next) {
n226=    if (%tvr.nextemp != $null) && (%tvr.nextepday == TBA) {
n227=      $sock(tvrage).mark  %tvr.showname :: Airs %tvr.airtime on %tvr.network  :: %tvr.nextepnum //  %tvr.nextepname %tvr.nextepday 
n228=      unset %tvr.* | halt
n229=    }
n230=    if (%tvr.nextemp != $null) && (Season isin %tvr.nextepname) && (%tvr.nextepday == TBA) {
n231=      $sock(tvrage).mark  %tvr.showname :: Airs %tvr.airtime on %tvr.network  :: %tvr.nextepnum //  %tvr.nextepname %tvr.nextepday 
n232=      unset %tvr.* | halt
n233=    }
n234=    if (%tvr.nextemp != $null) && (Season isin %tvr.nextepname) && (/ isin %tvr.nextepday) {
n235=      $sock(tvrage).mark  %tvr.showname :: Airs %tvr.airtime on %tvr.network  :: %tvr.nextepnum //  %tvr.nextepname $iif($left(%tvr.airnextepcmt,1) != -,::  %tvr.nextepday $+ : $betterduration(%tvr.airnextepcmt) ,::  %tvr.nextepday $betterduration(%tvr.airnextepcmt) ago. )
n236=      unset %tvr.* | halt
n237=    }
n238=    if (%tvr.nextemp != $null) {
n239=      $sock(tvrage).mark  %tvr.showname :: Airs %tvr.airtime on %tvr.network  :: %tvr.nextepnum // %tvr.nextepname $iif($left(%tvr.airnextepcmt,1) != -,::  %tvr.nextepday $+ : $betterduration(%tvr.airnextepcmt) ,::  %tvr.nextepday $betterduration(%tvr.airnextepcmt) ago. )
n240=      unset %tvr.* | halt
n241=    }
n242=    if (%tvr.nextemp == $null) {
n243=      $sock(tvrage).mark Next episode information for %tvr.showname is unavailable.
n244=      unset %tvr.* | halt
n245=    }
n246=  }
n247=
n248=  ; ============================
n249=  ; Last Episode
n250=  ; ============================
n251=
n252=  if (%tvr.output == last) {
n253=    if (%tvr.lastemp != $null) {
n254=      $sock(tvrage).mark %tvr.showname :: $iif(%tvr.ended != $null,Aired,Airs) %tvr.airtime on %tvr.network :: %tvr.lastepnum // %tvr.lastepname :: %tvr.lastepday :: https://broadcasthe.net/torrents.php?searchstr= $+ $replace(%tvr.showname,$chr(32),$chr(43)) $+ $chr(43) $+ s $+ $replace(%tvr.lastepnum,x,e)
n255=      unset %tvr.* | halt
n256=    }
n257=    if (%tvr.nextemp == $null) {
n258=      $sock(tvrage).mark Last episode information for $replace(%tvr.show,$chr(37) $+ 20,$chr(32)) is unavailable.
n259=      unset %tvr.* | halt
n260=    }
n261=  }
n262=
n263=  ; ============================
n264=  ; Show Information
n265=  ; ============================
n266=
n267=  if (%tvr.output == info) {
n268=    ; msg %tvr.output.chan
n269=    if (%tvr.showname != $null) {
n270=      $sock(tvrage).mark $chr(91) %tvr.showname $chr(93) :: %tvr.url $+ 
n271=      if (%tvr.ended != $null) {
n272=        $sock(tvrage).mark Premiered: %tvr.started - Ended: %tvr.ended 
n273=      }
n274=      else {
n275=        $sock(tvrage).mark Premiered: %tvr.started - %tvr.status ( $+ %tvr.country $+ ) $+ 
n276=      }
n277=      $sock(tvrage).mark Airs %tvr.airtime on %tvr.network ( $+ %tvr.runtime min $+ ) $+ 
n278=      $sock(tvrage).mark %tvr.genres $+ 
n279=      $sock(tvrage).mark Classification: %tvr.class $+ 
n280=      unset %tvr.* | halt
n281=    }
n282=    if (%tvr.showname == $null) {
n283=      $sock(tvrage).mark Show does not exist. Please check your spelling and try again.
n284=      unset %tvr.* | halt
n285=    }
n286=  }
n287=
n288=  ; ============================
n289=  ; Ep Information
n290=  ; ============================
n291=
n292=  if (%tvr.output == epsummary) {
n293=    sockopen -n tvrage2 www.tvrage.com 80
n294=  }
n295=
n296=  ; ============================
n297=  ; TVinfo Information
n298=  ; ============================
n299=
n300=  if (%tvr.output == showsummary) {
n301=    sockopen -n tvrage3 services.tvrage.com 80
n302=  }
n303=}
n304=
n305=on *:SOCKCLOSE:tvrage2: {
n306=
n307=  ; ============================
n308=  ; Ep Information
n309=  ; ============================
n310=
n311=  if (Click Here To Add isin %tvr.epsum) { notice %tvr.output.nick Sorry, no summary available for episode %tvr.epnumout of %tvr.showname }
n312=  else {
n313=    notice %tvr.output.nick $chr(91) %tvr.showname $chr(93) $chr(91) $+ 7 %tvr.epnumout - %tvr.epname  $+ $chr(93) $chr(91) $+ 7 %tvr.epair  $+ $chr(93) %tvr.url $+ 
n314=    notice %tvr.output.nick $nohtml(%tvr.epsum)
n315=    ;unset %tvr.* | halt
n316=  }
n317=}
n318=
n319=on *:SOCKCLOSE:tvrage3: {
n320=
n321=  ; ============================
n322=  ; Show Summary
n323=  ; ============================
n324=
n325=  query %tvr.output.nick $chr(91) %tvr.showname $chr(93)  %tvr.url $+ 
n326=  query %tvr.output.nick $replace(%tvr.showsummary,&quot;,")
n327=  query %tvr.output.nick $chr(91) Show image $chr(93) %tvr.showpic
n328=  ;unset %tvr.* | halt
n329=}
n330=
n331=; ===========================
n332=; Aliases
n333=; ===========================
n334=
n335=alias -l nohtml {
n336=  var %x, %i = $regsub($1-,/(^[^<]*>|<[^>]*>|<[^>]*$)/g,$null,%x), %x = $remove(%x,&nbsp;)
n337=  return %x
n338=}
n339=alias between { noop $regex($1,/\Q $+ $2 $+ \E(.*?)\Q $+ $3 $+ \E/gi) | return $regml($4) }
n340=
n341=; ==============================================
n342=; Thanks to http://www.nealirc.org/IRC/mIRC.html
n343=; ==============================================
n344=
n345=alias betterduration {
n346=  var %lifeseconds = $abs($1)
n347=  var %age, %years, %months, %days, %hours, %minutes, %seconds, %temp
n348=  %years = $int($calc(%lifeseconds / 31557600))
n349=  if (%years != 0) %age = %years year $+ $iif(%years > 1,s) $+ ,
n350=  %temp = $calc(%lifeseconds - (%years * 31557600))
n351=  %months = $int($calc(%temp / 2678400))
n352=  if (%months != 0) %age = %age %months month $+ $iif(%months > 1,s) $+ ,
n353=  %temp = $calc(%temp - (%months * 2678400)) 
n354=  %weeks = $int($calc(%temp / 604800))
n355=  if (%weeks != 0) %age = %age %weeks week $+ $iif(%weeks > 1,s) $+ ,
n356=  %temp = $calc(%temp - (%weeks * 604800))
n357=  %days = $int($calc(%temp / 86400))
n358=  if (%days != 0) %age = %age %days day $+ $iif(%days > 1,s) $+ ,
n359=  %temp = $calc(%temp - (%days * 86400))
n360=  %hours = $int($calc(%temp / 3600))
n361=  if (%hours != 0) %age = %age %hours hour $+ $iif(%hours > 1,s) $+ ,
n362=  %temp = $calc(%temp - (%hours * 3600))
n363=  %minutes = $int($calc(%temp / 60))
n364=  if (%minutes != 0) %age = %age %minutes minute $+ $iif(%minutes > 1,s)
n365=  %seconds = $calc(%temp % 60)
n366=  if (%seconds != 0) %age = %age and %seconds second $+ $iif(%seconds > 1,s)
n367=  if ($right(%age, 1) == $chr(44)) return $left(%age, -1)
n368=  return %age
n369=}
