[script]
n0=; Channel Control Dialog 2.0 by Amiga600 - To Replace The Crappy In-Built One :P
n1=; Posted on Hawkee Script Depository: http://www.hawkee.com/snippets/user/59854/
n2=; IRC:     irc.abjects.net       #mIRC    (Ford Lawnmower's Channel)
n3=;      and irc.millbrochat.net   #mIRC    (My Own IRC)
n4=
n5=; Best to save this as a separate file (eg. "ChanControl.mrc" then:
n6=; 1) /run explorer.exe $mircdir     - Paste the file into this directory
n7=; 2) /load -rs ChanControl.mrc      - Load's the file into mIRC
n8=; 3) Adjust Settings in CONFIG section below
n9=; 4) Right-Click in Channel, and Select Option "Channel Control"
n10=
n11=; End-Users may fork, change, alter, adjust this script to thier own tastes, If you edit this script
n12=; Please point users to this Original Script Location on Hawkee, and state you have modified your
n13=; version from the Original, I will not offer Help/Support for modified versions for obvious reasons!
n14=
n15=alias -l chancontconfig {
n16=  ; #### CONFIGURATION SECTION START ##
n17=
n18=  ; USER SETTINGS:
n19=  ; Enable Dialog Only When Channel Operator (1=Only Channel Operator / 0=Enable Always)
n20=  set %chancont.enableonlyifop 0
n21=
n22=  ; #### CONFIGURATION SECTION END - DO _NOT_ ALTER BELOW UNLESS YOU KNOW WHAT YOU'RE DOING ##
n23=}
n24=
n25=dialog chancontrol {
n26=  title "Channel Control"
n27=  size -1 -1 375 200
n28=  option dbu
n29=  tab "General Settings", 1, 8 7 360 166
n30=  edit "", 2, 17 64 188 57, tab 1 multi autovs limit 500
n31=  text "Channel Topic:", 3, 18 56 39 8, tab 1
n32=  text "Channel Creation Date:", 4, 18 33 102 8, tab 1
n33=  edit "-", 5, 17 42 189 10, tab 1 read autohs center
n34=  text "Channel Key:", 8, 262 33 48 8, tab 1
n35=  edit "", 9, 261 42 55 10, tab 1 limit 30 center
n36=  edit "", 10, 323 41 29 10, tab 1 limit 3 center
n37=  text "User Limit:", 11, 324 33 27 8, tab 1
n38=  text "Channel Modes:", 13, 215 33 41 8, tab 1
n39=  text "Topic Length:", 31, 124 124 34 8, tab 1
n40=  button "Update Channel Topic", 39, 139 54 65 10, tab 1
n41=  edit "", 12, 214 42 43 10, tab 1 limit 20 center
n42=  edit "-", 14, 159 123 45 10, tab 1 read center
n43=  button "Set Mode(s)", 15, 215 52 41 10, tab 1
n44=  button "Set", 16, 262 52 27 10, tab 1
n45=  button "Set Limit", 23, 324 52 27 10, tab 1
n46=  button "Un-Set", 24, 291 52 24 10, tab 1
n47=  text "Common Channel Modes:", 49, 213 74 109 8, tab 1
n48=  check "Protected Topic", 50, 214 89 65 10, tab 1
n49=  check "No External Messages", 51, 214 108 69 10, tab 1
n50=  check "Moderated", 52, 214 126 64 10, tab 1
n51=  check "Secret", 53, 312 89 50 10, tab 1
n52=  check "Private", 54, 312 108 50 10, tab 1
n53=  check "Invite Only", 55, 311 126 50 10, tab 1
n54=  edit "", 90, 88 157 264 11, tab 1 read multi autohs center
n55=  text "IRCd Version Information:", 66, 17 159 69 8, tab 1
n56=  text "Set By:", 67, 19 125 19 8, tab 1
n57=  text "Date:", 68, 18 137 18 8, tab 1
n58=  edit "-", 69, 39 124 82 10, tab 1 read center
n59=  edit "-", 70, 39 136 164 10, tab 1 read center
n60=  tab "Ban / Exception / Invite List's", 17
n61=  list 18, 12 32 115 105, disable tab 17 sort size extsel
n62=  list 19, 131 32 115 105, disable tab 17 sort size extsel
n63=  list 22, 249 32 115 105, disable tab 17 sort size extsel
n64=  text "Channel Ban List:", 26, 13 24 43 8, tab 17
n65=  text "Channel Exception List:", 27, 132 24 59 8, tab 17
n66=  text "Channel Invite List:", 28, 251 24 51 8, tab 17
n67=  edit "", 29, 57 142 220 10, tab 17 center
n68=  text "Edit Entry:", 33, 15 143 39 8, tab 17
n69=  button "Delete", 35, 317 157 44 12, disable tab 17
n70=  button "Add", 36, 317 141 44 12, disable tab 17
n71=  edit "", 37, 57 156 220 10, tab 17 read autohs center
n72=  text "Set By / Date:", 38, 15 158 39 8, tab 17
n73=  radio "Exception", 40, 279 150 35 10, disable tab 17
n74=  radio "Invite", 41, 279 160 30 10, disable tab 17
n75=  radio "Ban", 34, 279 140 24 10, disable tab 17
n76=  button "Re-Populate", 60, 72 22 39 9, tab 17
n77=  edit "-", 59, 112 22 15 10, tab 17 read center
n78=  edit "-", 61, 231 22 15 10, tab 17 read center
n79=  edit "-", 62, 349 22 15 10, tab 17 read center
n80=  tab "BlackListed Users", 6
n81=  text "BlackList On Join:", 7, 13 24 44 8, tab 6
n82=  list 25, 12 34 169 119, tab 6 size hsbar
n83=  text "Black List Ban's must always follow the USER!IDENT@HOST Format. Channel names must be separated by Spaces, and you can use #* to mean ALL Channels you are on.", 42, 191 128 169 22, tab 6
n84=  button "Add BlackList Ban", 43, 192 90 105 12, tab 6
n85=  button "Delete Selected BlackList Ban", 44, 95 156 89 12, disable tab 6
n86=  text "BlackList Ban:             ( NICKNAME   *OR*    USER ! IDENT @ HOST )", 46, 192 25 166 8, tab 6
n87=  text "Channels To Ban This User From:   (Eg. #Chat #Help or #* for ALL)", 47, 192 47 167 8, tab 6
n88=  edit "", 30, 191 35 170 10, tab 6 autohs center
n89=  edit "", 32, 191 55 170 10, tab 6 autohs center
n90=  check "Enable Delete Button", 48, 17 157 71 10, tab 6
n91=  edit "", 45, 191 75 170 10, tab 6 autohs limit 400 center
n92=  text "Blacklist Reason: (Shown when the user is Kicked Out)", 57, 192 67 164 8, tab 6
n93=  text "NOTE: You *MUST* be a Channel Operator or IRCop in order for this BlackList to work on your chosen Channels.", 56, 191 154 169 16, tab 6
n94=  check "Default Reason", 58, 310 85 50 10, tab 6
n95=  edit "-", 63, 166 23 15 10, tab 6 read center
n96=  button "Re-Populate", 64, 126 23 39 9, tab 6
n97=  text "NOTE: You can now get USER!IDENT@HOST of user by entering thier Nickname in 'BlackList Ban' and pressing 'Add BlackList Ban'", 65, 191 108 169 16, tab 6
n98=  button "OK", 20, 8 182 35 12, ok
n99=  button "Cancel", 21, 48 182 35 12, cancel
n100=  edit "", 99, 88 182 280 12, read autohs center
n101=}
n102=
n103=menu channel {
n104=  -
n105=  Channel Control 2.0:/chancont
n106=}
n107=
n108=#chancontroldialog off
n109=; Server-Support Numeric Handling
n110=raw $5:/^[^ ]+ (.*)are supported by this server/i:{ tokenize 32 $regml(1) | num5-add $* | haltdef }
n111=alias -l num5-add { hadd -m num5 $+($cid,_,$regsubex($1,/^(.*?)(=(.*))?$/,\1 $iif(\3,\3,$true))) }
n112=alias num5 { return $iif($hget(num5,$+($iif($2,$2,$cid),_,$1)),$v1,$false) }
n113=on *:DISCONNECT:{ if ($hget(num5)) { hdel -w num5 $+($cid,_*) } }
n114=alias ircversion {
n115=  if ($hget(num5)) { hdel -w num5 $+($cid,_*) }
n116=  .timer 1 1 !version
n117=}
n118=; Other Raw Numeric Handling
n119=raw *:*: {
n120=  if ($numeric == 352) {
n121=    ; /Who - This attempts to get U!I@H for BlackList Dialog
n122=    did -o chancontrol 30 1 $6 $+ ! $+ $3 $+ @ $+ $4
n123=    haltdef
n124=  }
n125=  if ($numeric == 351) {
n126=    ; IRCd Server Version (90=JunkBox)
n127=    did -o chancontrol 90 1 $2-
n128=    haltdef
n129=  }
n130=  if ($numeric == 332) {
n131=    ; Channel Topic
n132=    did -r chancontrol 2 1
n133=    did -o chancontrol 2 1 $3-
n134=    did -o chancontrol 14 1 $len($3-)
n135=    haltdef
n136=  }
n137=  if ($numeric == 333) {
n138=    ; Topic Set By / Date
n139=    did -o chancontrol 69 1 $3
n140=    did -o chancontrol 70 1 $asctime($4) ( $+  $duration($calc($ctime - $4)) ago)
n141=    haltdef
n142=  }  
n143=  if ($numeric == 324) {
n144=    ; Channel Modes
n145=    did -o chancontrol 12 1 $3-
n146=    if (t isincs $3-) did -c chancontrol 50 1 ;Channel Topic
n147=    elseif (t !isincs $3-) did -u chancontrol 50 1
n148=    if (n isincs $3-) did -c chancontrol 51 1 ;No External Messages
n149=    elseif (n !isincs $3-) did -u chancontrol 51 1
n150=    if (m isincs $3-) did -c chancontrol 52 1 ;Moderated
n151=    elseif (m !isincs $3-) did -u chancontrol 52 1
n152=    if (s isincs $3-) did -c chancontrol 53 1 ;Secret
n153=    elseif (s !isincs $3-) did -u chancontrol 53 1
n154=    if (p isincs $3-) did -c chancontrol 54 1 ;Private
n155=    elseif (p !isincs $3-) did -u chancontrol 54 1
n156=    if (i isincs $3-) did -c chancontrol 55 1 ;Invite
n157=    elseif (i !isincs $3-) did -u chancontrol 55 1
n158=  }
n159=  if ($numeric == 329) {
n160=    ; Channel Creation Date
n161=    did -o chancontrol 5 1 $asctime($3) ( $+  $duration($calc($ctime - $3)) ago)
n162=    haltdef
n163=  }
n164=  if ($numeric == 367) {
n165=    ; Channel Ban List
n166=    inc %chban 1
n167=    did -o chancontrol 18 %chban $3 ( $+ $4 $+ ) $asctime($5)
n168=    haltdef
n169=  }
n170=  if ($numeric == 348) {
n171=    ; Channel Exception List
n172=    inc %chexc 1
n173=    did -o chancontrol 19 %chexc $3 ( $+ $4 $+ ) $asctime($5)
n174=    haltdef
n175=  }
n176=  if ($numeric == 346) {
n177=    ; Channel Invite List
n178=    inc %chinv 1
n179=    did -o chancontrol 22 %chinv $3 ( $+ $4 $+ ) $asctime($5)
n180=    haltdef
n181=  }
n182=  if ($numeric == 368) {
n183=    ; End Of Channel Ban List
n184=    did -o chancontrol 59 1 %chban
n185=    haltdef
n186=  }
n187=  if ($numeric == 349) {
n188=    ; End Of Channel Except List
n189=    did -o chancontrol 61 1 %chexc
n190=    haltdef
n191=  }
n192=  if ($numeric == 347) {
n193=    ; End Of Channel Invite List
n194=    did -o chancontrol 62 1 %chinv
n195=    haltdef
n196=  }
n197=  if ($numeric == 472) || ($numeric == 482) {
n198=    ; Unknown Mode Character
n199=    did -o chancontrol 99 1 Error: $2-
n200=    haltdef
n201=  }
n202=}
n203=
n204=on 1:dialog:chancontrol:*:*: {
n205=  ; Debug Line
n206=  ;if ($devent != mouse) echo -a DNAME: $dname - DEVENT: $devent - DID: $did -  $did(chancontrol,18,0).seltext
n207=
n208=  if ($devent = sclick) {
n209=    if ($did = 34) {
n210=      ; Ban Checkmark
n211=      set %listmode b
n212=      did -eo chancontrol 36 1 Add Ban
n213=      did -eo chancontrol 35 1 Del Ban
n214=    }
n215=    if ($did = 40) {
n216=      ; Exception Checkmark
n217=      set %listmode e
n218=      did -eo chancontrol 36 1 Add Exception
n219=      did -eo chancontrol 35 1 Del Exception
n220=    }
n221=    if ($did = 41) {
n222=      ; Invite Checkmark
n223=      set %listmode I
n224=      did -eo chancontrol 36 1 Add Invite
n225=      did -eo chancontrol 35 1 Del Invite
n226=    }
n227=  }
n228=
n229=  if ($devent = edit) {
n230=    if ($did = 2) {
n231=      var %toplen $did(chancontrol,2,1).len 
n232=      inc %toplen $did(chancontrol,2,2).len
n233=      inc %toplen $did(chancontrol,2,3).len
n234=      inc %toplen $did(chancontrol,2,4).len
n235=      inc %toplen $did(chancontrol,2,5).len
n236=      inc %toplen $did(chancontrol,2,6).len
n237=      inc %toplen $did(chancontrol,2,7).len
n238=      inc %toplen $did(chancontrol,2,8).len
n239=      inc %toplen $did(chancontrol,2,9).len
n240=      var %toplendisp %toplen of $num5(TOPICLEN)
n241=      if ($num5(TOPICLEN) = $false) var %toplendisp %toplen
n242=      did -o chancontrol 14 1 %toplendisp
n243=    }
n244=  }
n245=  if ($devent = sclick) {
n246=
n247=    if ($did = 20) || ($did = 21) {
n248=      .disable #chancontroldialog
n249=      .timercccopy off
n250=      if ($hget(num5)) { hdel -w num5 $+($cid,_*) } ;Server-Numeric Handling
n251=    }
n252=    if ($did = 39) {
n253=      ; Update Channel Topic
n254=      var %chtopic $did(chancontrol,2,1) $+ $did(chancontrol,2,2) $+ $did(chancontrol,2,3) $+ $did(chancontrol,2,4) $+ $did(chancontrol,2,5) $+ $did(chancontrol,2,6) $+ $did(chancontrol,2,7)
n255=      if (%chtopic != $null) { /topic %chname %chtopic | did -o chancontrol 99 1 Channel %chname Topic Updated. }
n256=    }
n257=    if ($did = 15) {
n258=      ; Update Channel Modes
n259=      /mode %chname $did(chancontrol,12,1) | /mode %chname | did -o chancontrol 99 1 Channel %chname Modes Updated.   
n260=    }
n261=    if ($did = 16) {
n262=      ; Update Channel Key
n263=      /mode %chname +k $did(chancontrol,9,1) | /mode %chname | did -o chancontrol 99 1 Channel %chname Key Updated.
n264=    }
n265=    if ($did = 24) {
n266=      ; Unset Channel Key
n267=      /mode %chname -k $did(chancontrol,9,1) | /mode %chname | did -o chancontrol 99 1 Channel %chname Key/Password Removed.
n268=    }
n269=    if ($did = 23) {
n270=      var %chset 0
n271=      if ($did(chancontrol,10,1) > 0) && ($did(chancontrol,10,1) < 999) { var %chset 1 }
n272=      if (%chset = 0) { /mode %chname -l | did -o chancontrol 99 1 Channel %chname Limit Removed. }
n273=      if (%chset = 1) { /mode %chname +l $did(chancontrol,10,1) | did -o chancontrol 99 1 Channel %chname Limit Set To %chlim Users. }
n274=    }
n275=    if ($did = 18) {
n276=      ; Handle Ban List
n277=      set %listmode b
n278=      did -o chancontrol 29 1 $gettok($did(chancontrol,18).seltext,1,32)
n279=      did -o chancontrol 37 1 $gettok($did(chancontrol,18).seltext,2,32) on $gettok($did(chancontrol,18).seltext,3-,32)
n280=      did -c chancontrol 34 1
n281=      did -u chancontrol 40 1
n282=      did -u chancontrol 41 1
n283=      did -eo chancontrol 36 1 Add Ban
n284=      did -eo chancontrol 35 1 Del Ban
n285=    }
n286=    if ($did = 19) {
n287=      ; Exception List
n288=      set %listmode e
n289=      did -o chancontrol 29 1 $gettok($did(chancontrol,19).seltext,1,32)
n290=      did -o chancontrol 37 1 $gettok($did(chancontrol,19).seltext,2,32) on $gettok($did(chancontrol,19).seltext,3-,32)
n291=      did -u chancontrol 34 1
n292=      did -c chancontrol 40 1
n293=      did -u chancontrol 41 1
n294=      did -eo chancontrol 36 1 Add Exception
n295=      did -eo chancontrol 35 1 Del Exception
n296=    }
n297=    if ($did = 22) {
n298=      ; Invite List
n299=      set %listmode I
n300=      did -o chancontrol 29 1 $gettok($did(chancontrol,22).seltext,1,32)
n301=      did -o chancontrol 37 1 $gettok($did(chancontrol,22).seltext,2,32) on $gettok($did(chancontrol,22).seltext,3-,32)
n302=      did -u chancontrol 34 1
n303=      did -u chancontrol 40 1
n304=      did -c chancontrol 41 1
n305=      did -eo chancontrol 36 1 Add Invite
n306=      did -eo chancontrol 35 1 Del Invite
n307=    }
n308=
n309=    ; Ban List Multiple Selection Handler
n310=    if ($did(chancontrol,18,0).sel > 1) && (%listmode = b) {
n311=      var %banpop $null
n312=      var %banpopcnt 0
n313=      :banpopulate
n314=      inc %banpopcnt 1
n315=      var %banpop %banpop $+ $chr(32) $+ $gettok($did(chancontrol,18,$did(chancontrol,18,%banpopcnt).sel),1,32)
n316=      if (%banpopcnt < $did(chancontrol,18,0).sel) goto banpopulate
n317=      did -o chancontrol 29 1 %banpop
n318=      did -o chancontrol 37 1 Click On One Ban To Return *OR* To Remove Multiple Selections, Click DEL -->
n319=    }
n320=    ; Exception List Multiple Selection Handler
n321=    if ($did(chancontrol,19,0).sel > 1) && (%listmode = e) {
n322=      var %banpop $null
n323=      var %banpopcnt 0
n324=      :excpopulate
n325=      inc %banpopcnt 1
n326=      var %banpop %banpop $+ $chr(32) $+ $gettok($did(chancontrol,19,$did(chancontrol,19,%banpopcnt).sel),1,32)
n327=      if (%banpopcnt < $did(chancontrol,19,0).sel) goto excpopulate
n328=      did -o chancontrol 29 1 %banpop
n329=      did -o chancontrol 37 1 Click On One Exception To Return *OR* To Remove Multiple Selections, Click DEL -->
n330=    }
n331=    ; Invite List Multiple Selection Handler
n332=    if ($did(chancontrol,22,0).sel > 1) && (%listmode = I) {
n333=      var %banpop $null
n334=      var %banpopcnt 0
n335=      :invpopulate
n336=      inc %banpopcnt 1
n337=      var %banpop %banpop $+ $chr(32) $+ $gettok($did(chancontrol,22,$did(chancontrol,22,%banpopcnt).sel),1,32)
n338=      if (%banpopcnt < $did(chancontrol,22,0).sel) goto invpopulate
n339=      did -o chancontrol 29 1 %banpop
n340=      did -o chancontrol 37 1 Click On One Invite To Return *OR* To Remove Multiple Selections, Click DEL -->
n341=    }
n342=
n343=    if ($did = 36) {
n344=      ; Add Button
n345=      if (%listmode = b) {
n346=        set %chban 0
n347=        did -r chancontrol 18 1
n348=        .mode %chname + $+ $str(b,$numtok($did(chancontrol,29).text,32)) $did(chancontrol,29).text
n349=        .mode %chname +b
n350=        did -o chancontrol 99 1 Ban(s) Added: %banset
n351=      }
n352=      if (%listmode = e) {
n353=        set %chexc 0
n354=        did -r chancontrol 19 1
n355=        .mode %chname +  $+ $str(e,$numtok($did(chancontrol,29).text,32)) $did(chancontrol,29).text
n356=        .mode %chname +e
n357=        did -o chancontrol 99 1 Exception(s) Added: %banset
n358=      }
n359=      if (%listmode = I) {
n360=        set %chinv 0
n361=        did -r chancontrol 22 1
n362=        .mode %chname + $+ $str(I,$numtok($did(chancontrol,29).text,32)) $did(chancontrol,29).text
n363=        .mode %chname +I
n364=        did -o chancontrol 99 1 Invite(s) Added: %banset
n365=      }
n366=    }
n367=    if ($did = 35) {
n368=      ; Delete Button
n369=      if (%listmode = b) {
n370=        set %chban 0
n371=        did -r chancontrol 18 1
n372=        .mode %chname - $+ $str(b,$numtok($did(chancontrol,29).text,32)) $did(chancontrol,29).text
n373=        .mode %chname +b
n374=        did -o chancontrol 99 1 Channel %chname Ban %banset Deleted.
n375=        did -eo chancontrol 36 1 Add Ban
n376=        did -eo chancontrol 35 1 Del Ban
n377=      }
n378=      if (%listmode = e) {
n379=        set %chexc 0
n380=        did -r chancontrol 19 1
n381=        .mode %chname - $+ $str(e,$numtok($did(chancontrol,29).text,32)) $did(chancontrol,29).text
n382=        .mode %chname +e
n383=        did -o chancontrol 99 1 Channel %chname Exception %banset Deleted.
n384=        did -eo chancontrol 36 1 Add Exception
n385=        did -eo chancontrol 35 1 Del Exception
n386=      }
n387=      if (%listmode = I) {
n388=        set %chinv 0
n389=        did -r chancontrol 22 1
n390=        .mode %chname - $+ $str(I,$numtok($did(chancontrol,29).text,32)) $did(chancontrol,29).text
n391=        .mode %chname +I
n392=        did -o chancontrol 99 1 Channel %chname Invite %banset Deleted.
n393=        did -eo chancontrol 36 1 Add Invite
n394=        did -eo chancontrol 35 1 Del Invite
n395=      }
n396=      ; Reset MultiBan
n397=      did -o chancontrol 29 1 $null
n398=      did -o chancontrol 37 1 $null
n399=    }
n400=
n401=    if ($did = 43) {
n402=      ; Add To BlackList Button
n403=      var %bantype $did(chancontrol,30)
n404=      var %chans $did(chancontrol,32)
n405=      var %reason $did(chancontrol,45)
n406=      if ($len(%bantype) < 3) { did -o chancontrol 99 1 ERROR: Specify a Nickname on the Form First, If its not in U!I@H I will attempt to gain it.. | halt }
n407=      if (*!*@* !iswm %bantype) { did -o chancontrol 99 1 ERROR: Must be USER!IDENT@HOST format, Trying to Obtain U!I@H for user.. | .who %bantype | halt }
n408=      if ($chr(35) !isin %chans) { did -o chancontrol 99 1 ERROR: Must be Proper Channels, eg. #Lobby #Fred | halt }
n409=      if (%reason = $null) { did -o chancontrol 99 1 ERROR: You must either use the Default Reason or specify one! | halt }
n410=
n411=      did -o chancontrol 99 1 Checking BlackList..
n412=      var %tmp 0
n413=      did -r chancontrol 25 1
n414=      :ccread
n415=      inc %tmp 1
n416=      var %blk $read(ChanContBL12- $+ $network $+ .txt,%tmp)
n417=      var %frd $gettok(%blk,1,32)
n418=      did -o chancontrol 25 %tmp %blk
n419=      if (%frd = %bantype) { write -d ChanContBL12- $+ $network $+ .txt $did(25).seltext }
n420=      if (%frd != $null) goto ccread
n421=      write ChanContBL12- $+ $network $+ .txt $lower(%bantype) $str($chr(124),2) $lower(%chans) $str($chr(124),2) $fulldate $str($chr(124),2) %reason
n422=      did -o chancontrol 99 1 Added/Updated BlackList Entry Successfully.
n423=      /chancontupdateblacklist
n424=    }
n425=    if ($did = 44) {
n426=      ; Delete BlackList Entry
n427=      if ($exists(ChanContBL12- $+ $network $+ .txt)) {
n428=        write -dl $+ $did(25).sel ChanContBL12- $+ $network $+ .txt
n429=        did -b chancontrol 44
n430=        did -u chancontrol 48
n431=        did -o chancontrol 99 1 BlackList Entry Successfully Deleted.
n432=        /chancontupdateblacklist
n433=      }
n434=    }
n435=    if ($did = 64) {
n436=      ; Re-Populate BlackList
n437=      if ($exists(ChanContBL12- $+ $network $+ .txt)) {
n438=        did -o chancontrol 99 1 BlackList Re-Populated.
n439=        /chancontupdateblacklist
n440=      }
n441=    }
n442=    if ($did = 25) {
n443=      ; BlackList Selection
n444=      did -o chancontrol 30 1 $gettok($did(chancontrol,25).seltext,1,124)
n445=      did -o chancontrol 32 1 $gettok($did(chancontrol,25).seltext,2,124)
n446=      did -o chancontrol 45 1 $gettok($did(chancontrol,25).seltext,4,124)
n447=      did -b chancontrol 44
n448=      did -u chancontrol 48
n449=      did -n chancontrol 45 1
n450=      did -u chancontrol 58
n451=      did -o chancontrol 99 1 %cccopy
n452=      if ($gettok($did(chancontrol,25).seltext,1,124) != $null) { did -o chancontrol 99 1 BlackList Entry $gettok($did(chancontrol,25).seltext,1,124) was Added on $gettok($did(chancontrol,25).seltext,3,124) }
n453=    }
n454=    if ($did = 48) {
n455=      ; Enable BlackList Delete Button
n456=      if ($did(chancontrol,25).seltext = $null) { did -o chancontrol 99 1 Select A BlackList Entry Before Enabling Delete! | did -u chancontrol 48 | halt }
n457=      if ($did(chancontrol,48).state = 1) { did -e chancontrol 44 }
n458=      if ($did(chancontrol,48).state = 0) { did -b chancontrol 44 }
n459=    }
n460=    if ($did = 58) {
n461=      ; Default Blacklist Button
n462=      if ($did(chancontrol,58).state = 1) { did -m chancontrol 45 1 | did -o chancontrol 45 1 You Have Been BlackListed From This Channel - Good Day! }
n463=      if ($did(chancontrol,58).state = 0) { did -n chancontrol 45 1 | did -o chancontrol 45 1 }
n464=    }
n465=    if ($did = 50) {
n466=      ; Topic CheckBox
n467=      if ($did(chancontrol,50).state = 1) { .mode %chname +t | did -o chancontrol 99 1 Channel %chname Protected Topic Lock Enabled. }
n468=      if ($did(chancontrol,50).state = 0) { .mode %chname -t | did -o chancontrol 99 1 Channel %chname Protected Topic Lock Disabled. }
n469=      .mode %chname
n470=    }
n471=    if ($did = 51) {
n472=      ; No External Messages CheckBox
n473=      if ($did(chancontrol,51).state = 1) { .mode %chname +n | did -o chancontrol 99 1 Channel %chname No External Messages Enabled. }
n474=      if ($did(chancontrol,51).state = 0) { .mode %chname -n | did -o chancontrol 99 1 Channel %chname No External Messages Disabled. }
n475=      .mode %chname
n476=    }
n477=    if ($did = 52) {
n478=      ; Moderated CheckBox
n479=      if ($did(chancontrol,52).state = 1) { .mode %chname +m | did -o chancontrol 99 1 Channel %chname Moderated Mode Enabled. }
n480=      if ($did(chancontrol,52).state = 0) { .mode %chname -m | did -o chancontrol 99 1 Channel %chname Moderated Mode Disabled. }
n481=      .mode %chname
n482=    }
n483=    if ($did = 53) {
n484=      ; Secret CheckBox
n485=      if ($did(chancontrol,53).state = 1) { .mode %chname +s | did -o chancontrol 99 1 Channel %chname Secret Mode Enabled. }
n486=      if ($did(chancontrol,53).state = 0) { .mode %chname -s | did -o chancontrol 99 1 Channel %chname Secret Mode Disabled. }
n487=      .mode %chname
n488=    }
n489=    if ($did = 54) {
n490=      ; Private CheckBox
n491=      if ($did(chancontrol,54).state = 1) { .mode %chname +p | did -o chancontrol 99 1 Channel %chname Private Mode Enabled. }
n492=      if ($did(chancontrol,54).state = 0) { .mode %chname -p | did -o chancontrol 99 1 Channel %chname Private Mode Disabled. }
n493=      .mode %chname
n494=    }
n495=    if ($did = 55) {
n496=      ; Invite CheckBox
n497=      if ($did(chancontrol,55).state = 1) { .mode %chname +i | did -o chancontrol 99 1 Channel %chname Invite Mode Enabled. }
n498=      if ($did(chancontrol,55).state = 0) { .mode %chname -i | did -o chancontrol 99 1 Channel %chname Invite Mode Disabled. }
n499=      .mode %chname
n500=    }
n501=
n502=    if ($did = 60) {
n503=      ; Re-Populate Lists Button
n504=      did -o chancontrol 99 1 Re-Populated Channel Lists For %chname
n505=      if (b isincs $gettok($chanmodes,1,44)) { set %chban 0 | .mode %chname +b }
n506=      if (e isincs $gettok($chanmodes,1,44)) { set %chexc 0 | .mode %chname +e }
n507=      if (I isincs $gettok($chanmodes,1,44)) { set %chinv 0 | .mode %chname +I }
n508=    }
n509=  }
n510=}
n511=
n512=alias -l chancontupdateblacklist {
n513=  var %tmp 0
n514=  did -r chancontrol 25 1
n515=  /chancontsortbl ChanContBL12- $+ $network $+ .txt
n516=  :ccblackupd
n517=  inc %tmp 1
n518=  if ($exists(ChanContBL12- $+ $network $+ .txt)) {
n519=    var %blk $read(ChanContBL12- $+ $network $+ .txt,%tmp)
n520=    var %frd $gettok(%blk,1,124)
n521=    did -o chancontrol 25 %tmp %blk
n522=    if (%frd != $null) goto ccblackupd
n523=  }
n524=  var %cnt $calc(%tmp - 1)
n525=  did -o chancontrol 63 1 %cnt
n526=}
n527=alias chancontsortbl $iif($isfile($+(",$$1-,")),filter -affc $str($+(",$1-,",$chr(32)),2))
n528=
n529=#chancontroldialog end
n530=
n531=alias chancont {
n532=  if ($chan = $null) { echo -a ERROR: You must be on a Channel to call up the Channel Control Dialog!! | halt }
n533=  .enable #chancontroldialog
n534=  dialog $iif($dialog(chancontrol), -va, -m) chancontrol chancontrol
n535=  set %chban 0
n536=  set %chexc 0
n537=  set %chinv 0
n538=  set %chname $chan
n539=  .topic $chan
n540=  .mode $chan
n541=  /chancontconfig
n542=  set %cccopy Channel Control for Channel %chname - (C)2012 Amiga600 @ http://www.hawkee.com/snippets/user/59854/
n543=  did -o chancontrol 99 1 %cccopy
n544=  .timercccopy 0 30 did -o chancontrol 99 1 %cccopy
n545=  if (b isincs $gettok($chanmodes,1,44)) { .mode $chan +b | did -e chancontrol 18 | did -e chancontrol 34 1 | did -eo chancontrol 36 1 Add Ban | did -eo chancontrol 35 1 Del Ban | did -c chancontrol 34 1 }
n546=  elseif (b !isincs $gettok($chanmodes,1,44)) { did -o chancontrol 18 1 Un-Supported By The IRCd. }
n547=  if (e isincs $gettok($chanmodes,1,44)) { .mode $chan +e | did -e chancontrol 19 | did -e chancontrol 40 }
n548=  elseif (e !isincs $gettok($chanmodes,1,44)) { did -o chancontrol 19 1 Un-Supported By The IRCd. }
n549=  if (I isincs $gettok($chanmodes,1,44)) { .mode $chan +I | did -e chancontrol 22 | did -e chancontrol 41 }
n550=  elseif (I !isincs $gettok($chanmodes,1,44)) { did -o chancontrol 22 1 Un-Supported By The IRCd. }
n551=  set %listmode b
n552=  did -eo chancontrol 36 1 Add Ban
n553=  did -eo chancontrol 35 1 Del Ban
n554=  did -c chancontrol 34 1
n555=
n556=  if (%chancont.enableonlyifop = 1) {
n557=    if (!$nick(#,$me,~&@%)) { 
n558=      did -b chancontrol 36,35,2,39,52,51,50,53,54,55,12,15,16,24,9,10,23,34,40,41,60,29,18,19,22,25,30,32,45,58,43
n559=    }
n560=  }
n561=  /ircversion
n562=  /chancontupdateblacklist
n563=}
n564=on *:JOIN:#: {
n565=  /chancontconfig
n566=  if ($exists(ChanContBL12- $+ $network $+ .txt)) {
n567=    var %tmp 0
n568=    :cccheckbl
n569=    inc %tmp 1
n570=    var %blk $read(ChanContBL12- $+ $network $+ .txt,%tmp)
n571=    var %blentry $gettok(%blk,1,124)
n572=    var %blchans $lower($gettok(%blk,2,124))
n573=    var %blmsg $gettok(%blk,4,124)
n574=    if ($nick $+ !*@* = %blentry) { var %ban 1 }
n575=    if (%blentry iswm $address($nick,0)) { var %ban 1 }
n576=    if (%blentry iswm $address($nick,1)) { var %ban 1 }
n577=    if (%blentry iswm $address($nick,2)) { var %ban 1 }
n578=    if ($chan isin %blchans) || (#* isin %blchans) {
n579=      if (%ban = 1) { echo -a [BlackList] $nick on %blentry (Actual: $address($nick,5) $+ ) | .mode # +b %blentry | .kick # $nick BlackListed: %blmsg | set %chban 0 | .mode # +b | halt }
n580=    }
n581=    if (%blk != $null) goto cccheckbl
n582=  }
n583=}
